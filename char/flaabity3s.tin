#class {flaabity} {open}

#var guild jedi
#var subguild baadu

#gag {Your shot hit}
#gag {The Force flows through you, mending your wounds.}
#gag {The power of the Force soothes your pain.}
#gag {Your bleeding slows as you feel healing energy flow through you.}
#gag {Your bones knit together as the Force flows through you.}
#gag {The Force washes away your pain.}
#gag {Your wounds close as the Force courses through you.}
#gag {The power of the Force rejuvenates you.}
#gag {You feel your pain ebb.}
#gag {The power of the Force eases your pain.}
#gag {Your pain fades as the Force flows through you.}
#gag {The power of the Force soothes your pain.}
#gag {Your bruises fade as healing energy flows through you.}
#gag {The healing power of the Force eases your pain.}
#gag {The healing power of the Force clears your mind.}
#gag {Your thoughts gain strength as the healing energy flows through you.}
#gag {The power of the Force refreshes your mind.}
#gag {You reach out with the force and deflect the attack!}
#gag {The Force pours through your mind, strengthening it}
#gag {The Force flows through you, buttressing your thoughts.}
#gag {Your failing consciousness is bolstered by the soothing energy.}
#gag {You are not in need of aid.}
#gag {It is too soon to draw healing energies from the Force}
#gag {^%s$}
#gag {^You grasp at the Force within %1 to %2}
#gag {^You twist the Force and send %1 %2};
#gag {^The Force guides your actions.}
#gag {^You try and sidestep the attack and only partially succeed.}
#gag {^You try to leap aside and are sent sprawling in the dust.}
#gag {^You lose focus and miss deflecting the attack!}
#gag {^You spin aside but only manage to escape the worst of the attack.}
#gag {^You leap away from your opponent's attack!}
#gag {^You try to tumble to safety but get struck a glancing blow}
#gag {^Your mind is as strong as it can be.}
#gag {^pain.%s$}
#gag {^You spin away from a lethal blow!}
#gag {^You dive to the ground and roll away from your foe's attack!}
#gag {^You deftly sidestep a deadly strike!}
#gag {^Your thumbs are too tired to gouge right now}
#gag {^You feel rejuvenated by the power of the force}
#gag {^Raw power flows out of your body, causing it to shimmer with light.}
#gag {^You feel your body regenerating.}
#gag {^The clash of combat interrupts your concentration.}
#gag {^You feel rejuvenated by the power of the Force.}
#gag {^Your wounds mend as healing energy washes over you.}
#gag {^Your pain ebbs as your wounds mend.}
#gag {^Power flows through you, healing your wounds.}
#gag {^You feel your bones knitting together.}
#gag {^The healing power of the Force flows through you.}
#gag {^The Force courses through you, mending your body.}

#gag {^You clutch %1 heart with the Force}
#gag {^life from %1.}
#gag {^from %1.}

#gag {^You concentrate on your metabolism and free your body of}
#gag {^the harsh toxins within it.}

#gag {^You begin to control your pain.}

#gag {^You pull out your holocron and study it.}

#gag {^You prepare your mind to draw upon the force.}
#gag {^You already have your %1 drawn.}

#alias at {!focus dissipate on attack}
#alias c {#10 !focus confusion}
#alias co {!focus absorb energy}
#alias gfs {!focus greater force shield}
#alias gs {!focus guild stats /long}
#alias gs2 {!focus skill list /short}
#alias gs3 {!focus skill list /trainable}
#alias h {!focus reduce injury}
#alias jskills {!focus skill list}
#alias mb {!focus mindblock}
#alias pp {!focus cloud mind}
#alias pstats {!focus login stats}
#alias rcost {!focus check /cost}
#alias kk {kill $kstr}
#alias ka {focus force taunt for all};
#alias k {kill %0}
#alias lt talk %0
#alias ch com 
#alias chist com -h
#alias gwho jedis
#alias pt ptell %0
#alias ps members

#act {You are fully rejuvenated and thus exit your trance}{
	#var {my[cleanse][status]}{0};
};

#act {You have no need to enter a hibernation trance.}{
	#var {my[cleanse][status]}{0};
};

#act {You abruptly exit your hibernation trance.}{
	#var {my[cleanse][status]}{0};
};

#alias {ppp}{
	#10 {!focus cloud mind};
};



#alias {g}{
	!draw ${my[desired][weapon]};
	!focus mode /guide;
	#var {my[desired][mode]}{guide};
	#var {my[desired][modeshort]}{g};
};

#alias {fl}{
	!draw ${my[desired][weapon]};
	!focus mode /flurry;
	#var {my[desired][mode]}{flurry};
	#var {my[desired][modeshort]}{f};
};

#alias {de}{
	!draw ${my[desired][weapon]};
	!focus mode /deflect;
	#var {my[desired][mode]}{deflect};
	#var {my[desired][modeshort]}{d};
};

#alias {hh}{
	!sheath;
	!focus hand to hand;
	#var {my[desired][mode]}{hand to hand};
	#var {my[desired][modeshort]}{h};
	#var {my[desired][weapon]}{none};
};

#alias {ftik}{
	!draw ${my[desired][weapon]};
	!focus force trance /off;
	#if {"${my[desired][weapon]}" == "lightdaggers"}{
		#var {my[desired][mode]}{flurry};
		#var {my[desired][modeshort]}{f};
		!focus mode /flurry;
	}{
		#var {my[desired][mode]}{guide};
		#var {my[desired][modeshort]}{g};
		!focus mode /guide;
	};
	#var {my[desired][spell]}{injure-kill};
	#delay {2}{!focus force trance /ik,repeat};
};

#alias {fti}{
	!draw ${my[desired][weapon]};
	!focus force trance /off;
	#if {"${my[desired][weapon]}" == "lightdaggers"}{
		#var {my[desired][mode]}{flurry};
		#var {my[desired][modeshort]}{f};
		!focus mode /flurry;
	}{
		#var {my[desired][mode]}{guide};
		#var {my[desired][modeshort]}{g};
		!focus mode /guide;
	};
	#var {my[desired][spell]}{ipain};
	#delay {2}{!focus force trance /ip,repeat};
};

#alias {fttk}{
	!draw ${my[desired][weapon]};
	!focus force trance /off;
	!focus mode /flurry;
	#var {my[desired][mode]}{flurry};
	#var {my[desired][modeshort]}{f};
	#var {my[desired][spell]}{telekinetic kill};
	#delay {2}{!focus force trance /tk,repeat};
};

#alias {switchw}{
	#if {"${my[desired][weapon]}" == "lightsaber"}{
		#echo {Light Daggers};
		#var {my[desired][weapon]}{lightdaggers};
		fttk;
	}{
		#if {"${my[desired][weapon]}" == "lightdaggers"}{
			#echo {Light Staff};
			#var {my[desired][weapon]}{lightstaff};
			ftik;
		}{
			#echo {Light Saber};
			#var {my[desired][weapon]}{lightsaber};
		};
	};
	!sheath;
	!draw ${my[desired][weapon]};
};
			


#alias {ftd}{
	!draw ${my[desired][weapon]};
	!focus force trance /off;
	!focus mode /deflect;
	#var {my[desired][mode]}{deflect};
	#var {my[desired][modeshort]}{d};
	#var {my[desired][spell]}{ipain};
	#delay {2}{!focus force trance /ip,repeat};
};

#alias {ftr}{
	!focus force trance /off;
	#var {my[desired][spell]}{none};
};

#alias {show_damage_selector}{
	#draw {rounded}{box}{43 -41 55 -3}{       Jedi Damage Selector};
	#showme {Injure-Kill} 45 -38;
	#if {"${my[damage][type]}" == "boil"}{
		#showme {<faa> Boil    : Fire<088>} 46 -38;
	}{
		#showme { Boil    : Fire} 46 -38;
	};
	#button {46;-40;46;-4}{
		#send {alias ik focus injure-kill for boil};
		ftik;
		#var {my[damage][type]}{boil};
		show_damage_selector;
	};
	#if {"${my[damage][type]}" == "strangle"}{
		#showme {<faa> Strangle: Mind<088>} 47 -38;
	}{
		#showme { Strangle: Mind} 47 -38;
	};
	#button {47;-40;47;-4}{
		#send {alias ik focus injure-kill for strangle};
		ftik;
		#var {my[damage][type]}{strangle};
		show_damage_selector;
	};
	#if {"${my[damage][type]}" == "crumble"}{
		#showme {<faa> Crumble : Blunt<088>} 48 -38;
	}{
		#showme { Crumble : Blunt} 48 -38;
	};
	#button {48;-40;48;-4}{
		#send {alias ik focus injure-kill for crumble};
		ftik;
		#var {my[damage][type]}{crumble};
		show_damage_selector;
	};
	#showme {Telekinetic Kill} 50 -38;
	#if {"${my[damage][type]}" == "asphyxiate"}{
		#showme {<faa> Asphyxiate: Poison<088>} 51 -38;
	}{
		#showme { Asphyxiate: Poison} 51 -38;
	};
	#button {51;-40;51;-4}{
		#send {alias tk focus telekinetic kill for asphyxiate};
		fttk;
		#var {my[damage][type]}{asphyxiate};
		show_damage_selector;
	};
	#if {"${my[damage][type]}" == "cardiac"}{
		#showme {<faa> Cardiac   : Energy<088>} 52 -38;
	}{
		#showme { Cardiac   : Energy} 52 -38;
	};
	#button {52;-40;52;-4}{
		#send {alias tk focus telekinetic kill for cardiac};
		fttk;
		#var {my[damage][type]}{cardiac};
		show_damage_selector;
	};
	#if {"${my[damage][type]}" == "hemorrhage"}{
		#showme {<faa> hemorrhage: Mind<088>} 53 -38;
	}{
		#showme { Hemorrhage: Mind} 53 -38;
	};
	#button {53;-40;53;-4}{
		#send {alias tk focus telekinetic kill for hemorrhage};
		fttk;
		#var {my[damage][type]}{hemorrhage};
		show_damage_selector;
	};
	show_mode_selector;
};
#alias {show_mode_selector}{
	#draw {rounded}{box}{56 -41 64 -3}{       Jedi Mode Selector};
	#if {"${my[desired][mode]}" == "hand to hand" && "${my[desired][weapon]}" == "none"}{
		#showme {<faa>Bare Hands<088>} 58 -38;
	}{
		#showme {Bare Hands} 58 -38;
	};
	#button {58;-40;58;-20}{
		#var {my[desired][weapon]}{none};
		!sheath;
		#var {my[desired][mode]}{hand to hand};
		#var {my[desired][modeshort]}{h};
		!focus hand to hand;
		show_mode_selector;
	};
	#if {"${my[desired][mode]}" == "melee" && "${my[desired][weapon]}" == "none"}{
		#showme {<faa>Melee<088>} 59 -38;
	}{
		#showme {Melee} 59 -38;
	};
	#button {59;-40;59;-20}{
		#var {my[desired][weapon]}{none};
		!sheath;
		#var {my[desired][mode]}{melee};
		#var {my[desired][modeshort]}{m};
		!focus melee;
		show_mode_selector;
	};
	#if {"${my[desired][weapon]}" == "lightsaber"}{
		#showme {<faa>LightSaber<088>} 60 -38;
	}{
		#showme {LightSaber} 60 -38;
	};
	#button {60;-40;60;-20}{
		#var {my[desired][weapon]}{lightsaber};
		!sheath;
		!draw ${my[desired][weapon]};
		#var {my[desired][mode]}{guide};
		#var {my[desired][modeshort]}{g};
		!focus mode /guide;
		show_mode_selector;
	};
	#if {"${my[desired][weapon]}" == "lightstaff"}{
		#showme {<faa>LightStaff<088>} 61 -38;
	}{
		#showme {LightStaff} 61 -38;
	};
	#button {61;-40;61;-20}{
		#var {my[desired][weapon]}{lightstaff};
		!sheath;
		!draw ${my[desired][weapon]};
		#var {my[desired][mode]}{guide};
		#var {my[desired][modeshort]}{g};
		!focus mode /guide;
		show_mode_selector;
	};
	#if {"${my[desired][weapon]}" == "lightdaggers"}{
		#showme {<faa>LightDaggers<088>} 62 -38;
	}{
		#showme {LightDaggers} 62 -38;
	};
	#button {62;-40;62;-20}{
		#var {my[desired][weapon]}{lightdaggers};
		!sheath;
		!draw ${my[desired][weapon]};
		#var {my[desired][mode]}{flurry};
		#var {my[desired][modeshort]}{f};
		!focus mode /flurry;
		show_mode_selector;
	};
	#if {"${my[desired][mode]}" == "guide"}{
		#showme {<faa>guide<088>} 60 -18;
	}{
		#showme {guide} 60 -18;
	};
	#button {60;-18;60;-3}{
		#var {my[desired][mode]}{guide};
		!focus mode /guide;
		show_mode_selector;
	};
	#if {"${my[desired][mode]}" == "flurry"}{
		#showme {<faa>flurry<088>} 61 -18;
	}{
		#showme {flurry} 61 -18;
	};
	#button {61;-18;61;-3}{
		#var {my[desired][mode]}{flurry};
		!focus mode /flurry;
		show_mode_selector;
	};
	#if {"${my[desired][mode]}" == "deflect"}{
		#showme {<faa>deflect<088>} 62 -18;
	}{
		#showme {deflect} 62 -18;
	};
	#button {62;-18;62;-3}{
		#var {my[desired][mode]}{deflect};
		de;
		show_mode_selector;
	};
};

#nop Set default mode to ipain/guide
#delay {2}{alias ik focus injure-kill for boil;ftik}
#var {my[cleanse][status]}{0};
#var {my[cloud][threshold]}{50};
#var {my[desired][weapon]}{lightsaber};
#var {my[desired][mode]}{guide};
#var {my[damage][type]}{boil};
#ticker {damage_selector}{show_damage_selector}{4};
show_damage_selector


#nop {focus hpconfigure /HP: #hp/#mhp SP: #sp/#msp C: #c F: #f M: #sm/#%c D: #dn/#ng P: #df E: #es GXP: #gxp}
#gag {HP: %2/%3 SP: %4/%5 C: %6 F: %7 M: %8/%9 D:%10/%11 P: %12 E: %14GXP:%13}
#act {HP: %1/%2 SP: %3/%4 C: %5\% F: %6\% M: %7/%8\% D:%9/%10 P: %11 E:%12 GXP: %13\%} {
	#var {my[hp][current]}		{%1};
	#var {my[hp][max]}		{%2};
	#var {my[sp][current]}		{%3};
	#var {my[sp][max]}		{%4};
	#var {my[gp1][current]}		{%5};
	#var {my[conc][current]}	{%5};
	#var {my[gp2][current]}		{%6};
	#var {my[force][current]}	{%6};
	#var {my[mode][current]}	{%7};
	#var {my[charge][current]}	{%8};
	#var {my[dissipate][current]}	{%9};
	#var {my[energy][current]}	{%10};
	#var {my[prots][current][all]}	{%11};
	#var {my[attacker]}		{%12};
	#var {my[gtnl]}			{%13};
	#math {my[hp][deficit]}{$my[hp][max] - $my[hp][current]};
	#math {my[sp][deficit]}{$my[sp][max] - $my[sp][current]};
	#math {my[hp][percent]}{$my[hp][current] * 100 / $my[hp][max]};
	#math {my[sp][percent]}{$my[sp][current] * 100 / $my[sp][max]};
        #if {"${my[attacker]}" != " "}{
		#var {my[incombat]}	{1};
		#math {my[damagetracker][rounds]}{$my[damagetracker][rounds] + 1};
		#var {my[cleanse][status]}{0};
        }{
		#var {my[incombat]}	{0};
		#var {my[gouged]}	{0};
        };
	#if {${my[incombat]} == 1}{
		#if {$my[hp][deficit] > 150}{
			!focus reduce injury;
		};
		#if {$my[sp][deficit] > 100}{
			!focus clear mind;
		};
		#if {${my[hp][percent]} < 30}{
			go home;
		};
		#if {${my[gouged]} != 1}{
			!gouge;
			#var {my[gouged]}{1};
		};
	}{
		#if {${my[conc][current]} < 80 && ${my[cleanse][status]} == 0}{
			do_htrance
		};
		#if {${my[cleanse][status]} != 1}{
			#if {${my[hp][current]} < $my[hp][max]}{
				!focus reduce injury;
			};
			#if {${my[sp][current]} < $my[sp][max]}{
				!focus clear mind;
			};
		};
	};
	#if {${my[cleanse][status]} != 1}{
		#regex {$my[prots][current][all]}{s}{
			#var {my[prots][current][lfs]}{1};
		}{
			#var {my[prots][current][lfs]}{0};
			!focus lesser force shield;
		};
		#regex {$my[prots][current][all]}{S}{
			#var {my[prots][current][gfs]}{1};
		}{
			#var {my[prots][current][gfs]}{0};
			!focus greater force shield;
		};
		#regex {$my[prots][current][all]}{D}{
			#var {my[prots][current][dodge]}{1};
		}{
			#var {my[prots][current][dodge]}{0};
			!focus dodge /on;
		};
		#regex {$my[prots][current][all]}{RP}{
			#var {my[prots][current][rpoison]}{1};
		}{
			#var {my[prots][current][rpoison]}{0};
			!focus resist poison;
		};
		#if {"${my[mode][current]}" != "${my[desired][modeshort]}"}{
			#if {"${my[desired][modeshort]}" == "h"}{
				!sheath;
				!focus hand to hand;
			}{
				!sheath;
				!draw ${my[desired][weapon]};
				!focus mode /${my[desired][mode]};
			};
		};
		#if {"${my[mode][current]}" != "h"}{
			#if {$my[charge][current] < 5}{
				!smuggle remove cell;
				!recharge ${my[desired][weapon]};
			};
		};
		#regex {$mip[gline][2]}{Deflection UP}{
			#nop;
		}{
			!focus force deflection;
		};
		#regex {$mip[gline][2]}{Accelerated Healing}{
			#nop;
		}{
			!focus accelerated healing;
		};
	};
	#var {my[damagetracker][guildstring]}{${guild}/${subguild} - ${my[desired][weapon]} - ${my[desired][mode]}/${my[desired][spell]}/${my[damage][type]}};
	update_status;
}


#alias {update_status} {
	#var	{my[hp][graph]}		{@graph{$my[hp][current];$my[hp][max]}};
	#var	{my[sp][graph]}		{@graph{$my[sp][current];$my[sp][max]}};
	#var	{my[gp1][graph]}	{@graph{$my[gp1][current];100}};
	#var	{my[gp2][graph]}	{@graph{$my[gp2][current];100}};
	#var	{my[charge][graph]}	{@graph{$my[charge][current];100}};
	#var	{enemy[graph]}		{@graph{$enemy[hp];100}};
	#format {hp}			{%+4s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}		{%+4s} {<128>$my[hp][max]<088>};
	#format {sp}			{%+4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_max}		{%+4s} {<128>$my[sp][max]<088>};
	#format {conc}			{%+8s} {@colscale{$my[conc][current];100}};
	#format {force}			{%+6s} {@colscale{$my[force][current];100}};
	#format {charge}		{%+6s} {@colscale{$my[charge][current];100}};
	#format {ehealth}		{%+8s} {@colscale{$enemy[hp];100}\%};
	#showme {<088>HP:${hp}/${hp_max} SP:${sp}/${sp_max} C:${conc}%  F:${force}%    C: ${charge}%   M: ${my[desired][weapon]} - ${my[desired][mode]}/${my[desired][spell]}}{-4};
	#showme {H:${my[hp][graph]} S:${my[sp][graph]} C:${my[gp1][graph]} F:${my[gp2][graph]} C: ${my[charge][graph]}E:${enemy[graph]}} {-3};
	#showme {$mip[gline][2]} {-6};
	#showme {$mip[gline][1]} {-7};
}

#act {^You begin the meditation exercises to cleanse your soul!}{
	#var {my[cleanse][status]}{1};
};

#act {^Your meditation has cleared your soul of the pull of}{
	#var {my[cleanse][status]}{0};
};

#alias {do_cleanse}{
	!focus cleanse;
	#var {my[cleanse][status]} {1};
};

#alias {do_htrance}{
	!focus hibernation trance;
	#var {my[cleanse][status]}{1};
};

#alias {corpsetrig} {
	!wrap corpse;
	!focus absorb energy;
	!divvy;
	do_htrance;
}

#alias {cl}{
	do_cleanse;
};

#alias {ht}{
	do_htrance;
};

#alias {cco}{
	!unwrap corpse;
	#if {"$my[autocorpse][method]" == "guild"}{
		!focus absorb energy;
	};
	#if {"$my[autocorpse][method]" == "nex"}{
		!orare;
	};
};

#act {Flaabity dealt the killing blow to %1.} {
	log_damage_no_numbers %1;
	#if {"$my[autocorpse][method]" != "off"}{
		corpsetrig;
	};
}

#class {flaabity} {close}
